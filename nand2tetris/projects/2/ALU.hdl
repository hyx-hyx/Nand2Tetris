// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    
    // zx zy
    Mux16(a=x, b=false, sel=zx, out=zxout);
    Mux16(a=y, b=false, sel=zy, out=zyout);

    // nx,ny
    Not16(in=zxout, out=zxnotout);
    Not16(in=zyout, out=zynotout);

    Mux16(a=zxout, b=zxnotout, sel=nx, out=xnxout);
    Mux16(a=zyout, b=zynotout, sel=ny, out=ynxout);
    
    // f
    Add16(a=xnxout, b=ynxout, out=addout);
    And16(a=xnxout, b=ynxout, out=andout);
    Mux16(a=andout, b=addout, sel=f, out=fout);
    
    // no
    Not16(in=fout, out=fnotout);
    Mux16(a=fout, b=fnotout, sel=no, out=out,out=noout);
    
    //zr
    Or16Way(in=noout, out=zr1);
    Not(in=zr1, out=zr);
    
    
    //ng
    And16(a=noout, b[15]=true,b[0..14]=false,out=ng1);
    Or16Way(in=ng1,out=ng);
}